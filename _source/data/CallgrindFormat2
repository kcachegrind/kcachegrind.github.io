= 2. Reference

== 2.1 Grammar

A Callgrind profile data file is an expression of
"ProfileDataFile" in the following grammar
("+" means "at least once", "?" optional).

 ProfileDataFile := FormatSpec? FormatVersion? Creator? PartData*

 FormatSpec := "# callgrind format\n"
 
 FormatVersion := "version:" Space* Number "\n"

 Creator := "creator:" NoNewLineChar* "\n"

 PartData := (HeaderLine "\n")+ (BodyLine "\n")+

 HeaderLine := <i>(empty line)</i>
  | ('#' NoNewLineChar*)
  | PartDetail
  | Description
  | EventSpecification
  | CostLineDef

 PartDetail := TargetCommand | TargetID

 TargetCommand := "cmd:" Space* NoNewLineChar*

 TargetID := ("pid"|"thread"|"part") ":" Space* Number

 Description := "desc:" Space* Name Space* ":" NoNewLineChar*

 EventSpecification := "event:" Space* Name InheritedDef? LongNameDef?

 InheritedDef := "=" InheritedExpr

 InheritedExpr := Name
  | Number Space* ("*" Space*)? Name
  | InheritedExpr Space* "+" Space* InheritedExpr

 LongNameDef := ":" NoNewLineChar*

 CostLineDef := "events:" Space* Name (Space+ Name)*
  | "positions:" "instr"? (Space+ "line")?

 BodyLine := <i>(empty line)</i>
  | ('#' NoNewLineChar*)
  | CostLine
  | PositionSpecification
  | AssoziationSpecification

 CostLine := SubPositionList Costs?

 SubPositionList := (SubPosition+ Space+)+

 SubPosition := Number | "+" Number | "-" Number | "*"

 Costs := (Number Space+)+

 PositionSpecification := Position "=" Space* PositionName

 Position := CostPosition | CalledPosition

 CostPosition := "ob" | "fl" | "fi" | "fe" | "fn"

 CalledPosition := " "cob" | "cfl" | "cfn"

 PositionName := ( "(" Number ")" )? (Space* NoNewLineChar* )?

 AssoziationSpecification := CallSpezification
  | JumpSpecification

 CallSpecification := CallLine "\n" CostLine

 CallLine := "calls=" Space* Number Space+ SubPositionList

 JumpSpecification := ...

 Space := " " | "\t"

 Number := HexNumber | (Digit)+

 Digit := "0" | ... | "9"

 HexNumber := "0x" (Digit | HexChar)+

 HexChar := "a" | ... | "f" | "A" | ... | "F"

 Name = Alpha (Digit | Alpha)*

 Alpha = "a" | ... | "z" | "A" | ... | "Z"

 NoNewLineChar := <i>all characters without "\n"</i>

A profile data file ("ProfileDataFile") starts with basic information
such as a format marker, the version and creator information, and then
has a list of parts, where each part has its own header and
body. Parts typically are different threads and/or time spans/phases
within a profiled application run.</para>

Note that callgrind_annotate currently only supports profile data
files with one part. Callgrind may produce multiple parts for one
profile run, but defaults to one output file for each part.</para>

== 2.2 Description of Header Lines

Basic information in the first lines of a profile data file:

* <tt># callgrind format</tt> [Callgrind]<br>
This line specifies that the file is a callgrind profile, and it has
to be the first line. It was added late to the format (with Valgrind
3.13) and is optional, as all readers also should work with older
callgrind profiles not including this line.  However, generation of
this line is recommended to allow desktop environments and file
managers to uniquely detect the format.</para>

* <tt>version: number</tt> [Callgrind]<br>
This is used to distinguish future profile data file formats.  A
version of 1 is supposed to be upwards compatible with the Cachegrind
format.  Optional. If not given, version 1 is assumed.  Otherwise,
it has to follow directly after the format specification (i.e. be the
first line if the optional format specification is skipped)

* <tt>creator: string</tt> [Callgrind]<br>
Information about the creator of this file, i.e. the
profile tool or conversion script.
Optional.

* <tt>pid: process id</tt> [Callgrind]<br>
This specifies the process ID of the supervised
application for which this profile was generated.
Optional.

* <tt>cmd: program name + args</tt> [Cachegrind]<br>
This specifies the full command line of the supervised
application for which this profile was generated.
Optional.

* <tt>part: number</tt> [Callgrind]<br>
This specifies a sequentially running number for
profile dumps belonging to the same profile run of an application,
starting at 1.
Optional.

* <tt>desc: type: value</tt> [Cachegrind]<br>
This specifies various information for this dump.
For some types, the semantic is defined, but any
description type is allowed. Unknown types are
ignored.

== 2.3 Description of Body Lines

(to be continued)
